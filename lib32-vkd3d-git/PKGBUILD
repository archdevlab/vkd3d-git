# _     _            _        _          _____
#| |__ | | __ _  ___| | _____| | ___   _|___ /
#| '_ \| |/ _` |/ __| |/ / __| |/ / | | | |_ \
#| |_) | | (_| | (__|   <\__ \   <| |_| |___) |
#|_.__/|_|\__,_|\___|_|\_\___/_|\_\\__, |____/
#                                  |___/

#Maintainer: blacksky3 <https://github.com/blacksky3>
#Credits: Felix Yan <felixonmars@archlinux.or>
#Credits: Bruce Zhang
#Credits: Gustavo Alvarez <sl1pkn07@gmail.com>

pkgname=lib32-vkd3d-git
pkgver=1.2.r756.gbeafa091
pkgrel=1
pkgdesc='D3D12 to Vulkan translation library (32-bit) (git version)'
arch=(x86_64)
url='https://source.winehq.org/git/vkd3d.git'
license=(LGPL)
depends=(vkd3d lib32-vulkan-icd-loader lib32-libxcb)
makedepends=(spirv-headers vulkan-headers xcb-proto wine gcc gcc-libs lib32-gcc-libs xorgproto xcb-util-keysyms lib32-xcb-util-keysyms)
depends=(vkd3d lib32-vulkan-icd-loader lib32-libxcb)
conflicts=(lib32-vkd3d)
provides=(lib32-vkd3d lib32-vkd3d-git)
source=(git://source.winehq.org/git/vkd3d.git)
md5sums=(SKIP)

pkgver(){
  cd ${srcdir}/vkd3d

  # cutting off 'foo-' prefix that presents in the git tag
  git describe --long --tags | sed 's/^vkd3d-//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare(){
  cd ${srcdir}/vkd3d

  ./autogen.sh
}


build(){
export CC='gcc -m32'
export CXX='g++ -m32'
export ASFLAGS=--32
export CFLAGS=-m32
export CXXFLAGS=-m32
export PKG_CONFIG_PATH=/usr/lib32/pkgconfig

  cd ${srcdir}/vkd3d

  # with-spirv-tools build
  #./configure --build=i386-pc-linux-gnu CFLAGS=-m32 CXXFLAGS=-m32 LDFLAGS=-m32 --prefix=/usr --libdir=/usr/lib32 --with-spirv-tools --enable-tests=no --disable-doxygen-doc

  # with no spirv-tools enabled
  ./configure --build=i386-pc-linux-gnu CFLAGS=-m32 CXXFLAGS=-m32 LDFLAGS=-m32 --prefix=/usr --libdir=/usr/lib32 --enable-tests=no --disable-doxygen-doc

  make -j$(nproc)
}

package(){
  make DESTDIR="$pkgdir" -C ${srcdir}/vkd3d/ install

  rm -fr "${pkgdir}"/usr/include

  (cd "${pkgdir}/usr/bin"; for i in *; do mv "${i}" "${i}-32"; done)

  # installing license
  install -Dt "$pkgdir"/usr/share/licenses/"$pkgname" -m644 "$srcdir"/vkd3d/LICENSE
  install -Dt "$pkgdir"/usr/share/licenses/"$pkgname" -m644 "$srcdir"/vkd3d/COPYING
  install -Dt "$pkgdir"/usr/share/licenses/"$pkgname" -m644 "$srcdir"/vkd3d/AUTHORS
}
