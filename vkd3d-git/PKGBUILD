#Maintainer: archdevlab <https://github.com/archdevlab>
#Credits: Felix Yan <felixonmars@archlinux.or>
#Credits: Bruce Zhang
#Credits: Gustavo Alvarez <sl1pkn07@gmail.com>

pkgname=vkd3d-git
pkgdesc='D3D12 to Vulkan translation library (git version)'
pkgver=1.12.r132.g5a53b73
pkgrel=1
arch=(x86_64)
url='https://source.winehq.org/git/vkd3d.git'
license=(LGPL)
depends=(vulkan-icd-loader)
makedepends=(spirv-headers vulkan-headers xcb-util-keysyms xorgproto wine git)
conflicts=(vkd3d)
provides=(vkd3d vkd3d-git)
source=(git+https://gitlab.winehq.org/wine/vkd3d.git)

pkgver(){
  cd "${srcdir}"/vkd3d
  git describe --long --tags --abbrev=7 | sed 's/^vkd3d-//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare(){
  cd "${srcdir}"/vkd3d

  ./autogen.sh
}

build(){
  cd "${srcdir}"/vkd3d

  # with no spirv-tools
  ./configure --prefix=/usr --enable-tests=no

  # with spirv-tools
  #./configure --prefix=/usr --with-spirv-tools --enable-tests=no

  make -j$(nproc)
}

package(){
  make DESTDIR="${pkgdir}" -C "${srcdir}"/vkd3d install

  # install license
  install -dm755 "${pkgdir}"/usr/share/licenses/"${pkgname}"
  install -m644 "${srcdir}"/vkd3d/LICENSE "${pkgdir}"/usr/share/licenses/"${pkgname}"/
  install -m644 "${srcdir}"/vkd3d/COPYING "${pkgdir}"/usr/share/licenses/"${pkgname}"/
  install -m644 "${srcdir}"/vkd3d/AUTHORS "${pkgdir}"/usr/share/licenses/"${pkgname}"/
}

sha256sums=('SKIP')

# vim:set ts=8 sts=2 sw=2 et:
