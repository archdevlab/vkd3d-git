#Maintainer: archdevlab <https://github.com/archdevlab>
#Credits: Felix Yan <felixonmars@archlinux.or>
#Credits: Bruce Zhang
#Credits: Gustavo Alvarez <sl1pkn07@gmail.com>

pkgname=lib32-vkd3d-git
pkgver=1.12.r132.g5a53b73
pkgrel=1
pkgdesc='D3D12 to Vulkan translation library (32-bit) (git version)'
arch=(i686 x86_64)
url='https://source.winehq.org/git/vkd3d.git'
license=(LGPL)
depends=(lib32-vulkan-icd-loader)
makedepends=(spirv-headers vulkan-headers lib32-xcb-util-keysyms lib32-ncurses xorgproto wine git)
conflicts=(lib32-vkd3d)
provides=(lib32-vkd3d lib32-vkd3d-git)
source=(git+https://gitlab.winehq.org/wine/vkd3d.git)

pkgver(){
  cd "${srcdir}"/vkd3d
  git describe --long --tags --abbrev=7 | sed 's/^vkd3d-//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare(){
  cd "${srcdir}"/vkd3d

  ./autogen.sh
}

build(){
export CFLAGS+=" -m32"
export CXXFLAGS+=" -m32"
export LDFLAGS+=" -m32"
export LDFLAGS+=" -L/usr/lib32"
export ASFLAGS+=" --32"
export PKG_CONFIG_PATH=/usr/lib32/pkgconfig

  cd "${srcdir}"/vkd3d

  ./configure --build=i686-pc-linux-gnu --prefix=/usr --libdir=/usr/lib32 --enable-tests=no --disable-doxygen-doc

  make -j$(nproc)
}

package(){
  make DESTDIR="${pkgdir}" -C "${srcdir}"/vkd3d install

  rm -fr "${pkgdir}"/usr/include

  (cd "${pkgdir}"/usr/bin; for i in *; do mv "${i}" "${i}-32"; done)

  # install license
  install -dm755 "${pkgdir}"/usr/share/licenses/"${pkgname}"
  install -m644 "${srcdir}"/vkd3d/LICENSE "${pkgdir}"/usr/share/licenses/"${pkgname}"/
  install -m644 "${srcdir}"/vkd3d/COPYING "${pkgdir}"/usr/share/licenses/"${pkgname}"/
  install -m644 "${srcdir}"/vkd3d/AUTHORS "${pkgdir}"/usr/share/licenses/"${pkgname}"/
}

sha256sums=('SKIP')

# vim:set ts=8 sts=2 sw=2 et:
