# _     _            _        _          _____
#| |__ | | __ _  ___| | _____| | ___   _|___ /
#| '_ \| |/ _` |/ __| |/ / __| |/ / | | | |_ \
#| |_) | | (_| | (__|   <\__ \   <| |_| |___) |
#|_.__/|_|\__,_|\___|_|\_\___/_|\_\\__, |____/
#                                  |___/

#Maintainer: blacksky3 <https://github.com/blacksky3>
#Credits: Felix Yan <felixonmars@archlinux.or>
#Credits: Bruce Zhang
#Credits: Gustavo Alvarez <sl1pkn07@gmail.com>

pkgname=vkd3d-git
pkgdesc='D3D12 to Vulkan translation library (git version)'
pkgver=1.2.r756.gbeafa091
pkgrel=1
arch=(x86_64)
url='https://source.winehq.org/git/vkd3d.git'
license=(LGPL)
depends=(spirv-tools vulkan-icd-loader libxcb)
makedepends=(spirv-headers vulkan-headers xcb-proto wine gcc gcc-libs xorgproto xcb-util-keysyms git)
conflicts=(vkd3d)
provides=(vkd3d vkd3d-git)
source=(git+https://gitlab.winehq.org/wine/vkd3d.git)

pkgver(){
  cd ${srcdir}/vkd3d

  # cutting off 'foo-' prefix that presents in the git tag
  git describe --long --tags --abbrev=7 | sed 's/^vkd3d-//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare(){
  cd ${srcdir}/vkd3d

  ./autogen.sh
}


build(){
  cd ${srcdir}/vkd3d

  ./configure --prefix=/usr --with-spirv-tools --enable-tests=no

  make -j$(nproc)
}

package(){
  make DESTDIR="$pkgdir" -C ${srcdir}/vkd3d/ install

  # installing license
  install -Dt "$pkgdir"/usr/share/licenses/"$pkgname" -m644 "$srcdir"/vkd3d/LICENSE
  install -Dt "$pkgdir"/usr/share/licenses/"$pkgname" -m644 "$srcdir"/vkd3d/COPYING
  install -Dt "$pkgdir"/usr/share/licenses/"$pkgname" -m644 "$srcdir"/vkd3d/AUTHORS
}

sha256sums=('SKIP')
